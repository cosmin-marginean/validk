plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.dokka" version "1.7.20"
    id "jacoco"
    id "com.github.nbaztec.coveralls-jacoco" version "1.2.15"
    id "maven-publish"
    id "signing"
    id "com.glovoapp.semantic-versioning" version "1.1.10"
}

repositories {
    mavenCentral()
}

ext.publishDescription = "A validation library for Kotlin (JVM)"
apply from: 'publish.gradle'

group = "io.resoluteworks"

dependencies {
    testImplementation "io.kotest:kotest-assertions-core:${kotestVersion}"
    testImplementation "io.kotest:kotest-property:${kotestVersion}"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:${kotestVersion}"
}

kotlin {
    jvmToolchain(17)
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadocJar {
    dependsOn(dokkaHtml)
    archiveClassifier.set("javadoc")
    from(dokkaHtml.outputDirectory)
}

dokkaHtml {
    outputDirectory.set(file("../docs/dokka/${project.name}"))
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    finalizedBy jacocoTestReport
}

signing {
    sign publishing.publications.mavenJava
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

coverallsJacoco {
    reportPath = "validk/build/reports/jacoco/test/jacocoTestReport.xml"
}

tasks.coverallsJacoco.dependsOn(jacocoTestReport)
tasks.publish.dependsOn(test)
